/*
 * Copyright (c) 2016. Manuel Rebollo BÃ¡ez.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'

apply from: 'config/gradle/jacoco.gradle'

def STRING = "String"

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.mrebollob.restaurant"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode computeVersionCode()
        versionName "0.9"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField STRING, "APP_URL", "\"https://play.google.com/apps/testing/com.mrebollob.restaurant\""
    }

    buildTypes {
        debug {
            applicationIdSuffix '.develop'
            buildConfigField STRING, "GIT_SHA", "\"DEBUG\""
            buildConfigField STRING, "BUILD_TIME", "\"DEBUG\""
            debuggable true
            minifyEnabled false
            testCoverageEnabled true
        }

        release {
            def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
            def buildTime = new GregorianCalendar().format("MM-dd-yyyy' 'h:mm:ss a z")
            buildConfigField STRING, "GIT_SHA", "\"${gitSha}\""
            buildConfigField STRING, "BUILD_TIME", "\"${buildTime}\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
    }
}
kapt {
    generateStubs = true
}

dexcount {
    format = "list"
    includeClasses = false
    includeFieldCount = true
    includeTotalMethodCount = true
    orderByMethodCount = false
    verbose = true
    maxTreeDepth = Integer.MAX_VALUE
    enableForInstantRun = false
    runOnEachAssemble = false
}

dependencies {
    // Loading view

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true;
    }
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support:design:25.3.0'
    compile 'com.github.satyan:sugar:1.4'

    compile 'com.victor:lib:1.0.4'
    compile 'com.github.cooltechworks:CreditCardView:v1.0.2'
    compile 'com.andrognito.pinlockview:pinlockview:1.0.1'
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'
    compile 'io.reactivex.rxjava2:rxjava:2.0.2'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.squareup.okhttp3:okhttp:3.4.2'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.2'
    compile 'org.jsoup:jsoup:1.10.1'

    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.google.dagger:dagger:2.5'
    kapt 'com.google.dagger:dagger-compiler:2.5'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    testCompile 'junit:junit:4.12'
    testCompile 'com.nhaarman:mockito-kotlin:1.1.0'
    testCompile 'org.assertj:assertj-core:3.6.1'
}
repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "http://repository.jetbrains.com/all" }
    maven { url 'https://maven.fabric.io/public' }
}

/**
 * Gets the commit number from the current Git commit. E.g. 538
 */
def computeVersionCode() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--count', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim().toInteger()
}